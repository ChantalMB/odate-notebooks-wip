{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Introduction to SPARQL, RDF, and LOD\n",
    "\n",
    "While many databases, services, or museums might expose their data via a web API, there can be limitations. Matthew Lincoln has an excellent tutorial at [The Programming Historian](https://programminghistorian.org/en/lessons/graph-databases-and-SPARQL) that walks us through some of these differences, but the key one is in the way the data is represented. When data is described using a 'Resource Description Framework', RDF, the resource - the 'thing'- is described via a series of relationships, rather than as rows in a table or keys having values. \n",
    "\n",
    "Information is in the relationships. It's a network. It's a _graph_. Thus, every 'thing' in this graph can have its own _uniform resource identifier_ (URI) that lives as a location on the internet. Information can then be created by making _statements_ that use these URIs, similarly to how English grammar creates meaning: subject verb object. Or, in RDF-speak, 'subject predicate object', also known as a _triple_. In this way, data in _different_ places can be linked together by referencing the elements they have in common. This is Linked Open Data (LOD). The access point for interrogating LOD is called an 'endpoint'. \n",
    "\n",
    "Finally, _SPARQL_ is an acronymn for SPARQL Protocol and RDF Query Language (yes, it's one of _those_ kinds of acronyms).\n",
    "\n",
    "In this notebook, we're not using Python or R directly. Instead, we've set up a 'kernel' (think of that as the 'engine' for the notebook) that already includes everything necessary to set up and run SPARQL queries. (For reference, the kernel code is [here](https://github.com/paulovn/sparql-kernel)). Both R and Python can interact with and query endpoints, and manipulate linked open data, but for the sake of learning a bit of what one can do with SPARQL, this notebook keeps all of that ancillary code tucked away. The [followup notebook](Using R to Retrieve and Visualize Data from SPARQL.ipynb) to this one shows you how to use R to do some basic manipulations of the query results."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "## Simple RDF example\n",
    "\n",
    "Here, we are following [Matthew Lincoln's tutorial](https://programminghistorian.org/en/lessons/graph-databases-and-SPARQL).  \n",
    "\n",
    "Let's look at his example, which concerns the painting, 'The Nightwatch'. \n",
    "\n",
    "`<The Nightwatch> <was created by> <Rembrandt van Rijn> .`\n",
    "\n",
    "This _statement_ has three elements: \n",
    "\n",
    "+ the subject: `<The Nightwatch>`\n",
    "+ the predicate: `<was created by>`\n",
    "+ the object: `<Rembrandt van Rijn>`\n",
    "\n",
    "Lincoln combines these, and other such statements, into a (pseudo-)RDF database like so:\n",
    "\n",
    "```\n",
    "<The Nightwatch> <was created by> <Rembrandt van Rijn> .\n",
    "<The Nightwatch> <was created in> <1642> .\n",
    "<The Nightwatch> <has medium> <oil on canvas> .\n",
    "<Rembrandt van Rijn> <was born in> <1606> .\n",
    "<Rembrandt van Rijn> <has nationality> <Dutch> .\n",
    "<Johannes Vermeer> <has nationality> <Dutch> .\n",
    "<Woman with a Balance> <was created by> <Johannes Vermeer> .\n",
    "<Woman with a Balance> <has medium> <oil on canvas> .\n",
    "```\n",
    "\n",
    "Such RDF databases are describing nodes and links, and so we can visualize as a graph like so:\n",
    "\n",
    "![A network visualization of the pseudo-RDF shown above. Arrows indicate the ‘direction’ of the predicate. For example, that ‘Woman with a Balance was created by Vermeer’, and not the other way around.](https://programminghistorian.org/images/graph-databases-and-SPARQL/sparql01.svg)\n",
    "\n",
    "But there is a difference between the pseudo-RDF that Lincoln shows us, and what _actual_ RDF might look like:\n",
    "\n",
    "```\n",
    "<http://data.rijksmuseum.nl/item/8909812347> <http://purl.org/dc/terms/creator>  <http://dbpedia.org/resource/Rembrandt>\n",
    "```\n",
    "\n",
    "The human-readable version requires _more_ statements:\n",
    "\n",
    "```\n",
    "<http://data.rijksmuseum.nl/item/8909812347> <http://purl.org/dc/terms/title> \"The Nightwatch\" .\n",
    "\n",
    "<http://purl.org/dc/terms/creator> <http://www.w3.org/1999/02/22-rdf-syntax-ns#label> \"was created by\" .\n",
    "\n",
    "<http://dbpedia.org/resource/Rembrandt> <http://xmlns.com/foaf/0.1/name> \"Rembrandt van Rijn\" .\n",
    "```\n",
    "\n",
    "This is just a quick introduction; please do examine [Lincoln's tutorial](https://programminghistorian.org/en/lessons/graph-databases-and-SPARQL) for more details. But now, let's explore how this notebook can be used to write some queries.\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "# Jupyter notebooks have various built-in commands called 'magics' that are accessed with the '%' character; these depend on the kernel. \n",
    "# Let's see what the SPARQL kernel has\n",
    "%lsmagics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "# when using this notebook, the first thing we have to do - or rather, the first time we run _any_ query,\n",
    "# is to tell it what endpoint we're going to use. Let's use the British Museum's:\n",
    "\n",
    "%endpoint http://edan.si.edu/saam/sparql"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Lincoln suggests that when we first encountered a new RDF graph, that we explore the network of relationships from an example object to understand what is going on in the database, to see what is available for querying. Since we're querying the British Museum, let's take [the Rosetta Stone](http://collection.britishmuseum.org/id/object/YCA62958) as our example.\n",
    "\n",
    "In the query below, `p` and `o` stand for 'predicate' and 'object'. Thus, we're building up a query that asks, 'show me every statment structured `<The Rosetta Stone> <predicate> <object>`. When the results load up, you can right-click on each statement (which is a URI, remember) to see what we've discovered. This could give you the necessary information to construct more complicated queries.\n",
    "\n",
    "*Nb* The British Museum sparql endpoint and the underlying infrastructure does not appear to be well supported. Results are sometimes flaky or not reachable."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true,
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "SELECT ?p ?o\n",
    "WHERE {\n",
    "  <http://edan.si.edu/saam/id/object/1980.71> ?p ?o .\n",
    "}\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In this next query, we look for objects in the collection that have the label 'fibula'."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true,
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "%endpoint http://edan.si.edu/saam/sparql\n",
    "%display table\n",
    "PREFIX bmo: <http://www.researchspace.org/ontology/>\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "\n",
    "SELECT ?object\n",
    "WHERE {\n",
    "\n",
    "  # Search for all values of ?object that have a given \"object type\"\n",
    "  ?object bmo:PX_object_type ?object_type .\n",
    "\n",
    "  # That object type should have the label \"fibula\"\n",
    "  ?object_type skos:prefLabel \"fibula\" .\n",
    "}\n",
    "LIMIT 10"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Wikidata\n",
    "\n",
    "Wikidata is another endpoint we can query. Below we have a query by Sebastian Heath that extracts some of the genealogical data on Roman emperors contained in that database. The `wd:Q842606` can be expanded to refer to [https://www.wikidata.org/wiki/Q842606](https://www.wikidata.org/wiki/Q842606), which describes the concept 'Roman Emperor'. `wdt:P39` is a predicate meaning 'Position held' [https://www.wikidata.org/wiki/Property:P39](https://www.wikidata.org/wiki/Property:P39). "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true,
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "%endpoint http://query.wikidata.org/sparql\n",
    "%display table\n",
    "\n",
    "SELECT ?emperorLabel ?emperor_dob\n",
    "       ?childLabel\n",
    "       ?motherLabel ?maternalGrandfatherLabel ?maternalGrandmotherLabel\n",
    "       ?emperor ?child ?mother ?maternalGrandfather ?maternalGrandmother WHERE {\n",
    "  \n",
    "  ?emperor wdt:P39 wd:Q842606 . #p39: position held. Q842606: Roman Emperor\n",
    "  ?emperor wdt:P569 ?emperor_dob . # p569: date of birth\n",
    "  ?child wdt:P22 ?emperor . #p22: father\n",
    "  ?child wdt:P25 ?mother .  #p25: mother\n",
    "  OPTIONAL { ?mother wdt:P22 ?maternalGrandfather }\n",
    "  OPTIONAL { ?mother wdt:P25 ?maternalGrandmother }\n",
    "  \n",
    " # automatic label expander\n",
    " SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n",
    " \n",
    "} ORDER BY ?emperor_dob\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let's visualize these relationships. We're running the same query, but we use CONSTRUCT to create the nodes and edges that represent these familial relationships. We want to show 'emperor x is the father of person y' and 'person a is the mother of person y'.  That gives us the structure. To get the content, we run the SELECT command where we first tell it to retrieve those individuals who were emperor, and then retrieve the children data.  \n",
    "\n",
    "Once you've run the query, use ctrl+f to find someone familiar, like Augustus (Q1405). In the resulting graph, an edge labeled 'p22' eg Q1405 ->P22 -> Q2259 can be read, 'Q1405 is the father of Q2259', or rather, 'Augustus is the father of Julia the Elder'.\n",
    "\n",
    "Roman geneaology.... it's complicated! "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "%endpoint http://query.wikidata.org/sparql\n",
    "%display diagram \n",
    "\n",
    "CONSTRUCT {\n",
    "         ?emperor wdt:P22 ?child . #p22: father\n",
    "         ?mother wdt:P25 ?child .  #p25: mother \n",
    "          }\n",
    "\n",
    "WHERE {\n",
    "  \n",
    "  ?emperor wdt:P39 wd:Q842606 .\n",
    "  ?child wdt:P22 ?emperor . #p22: father\n",
    "  ?child wdt:P25 ?mother .  #p25: mother\n",
    "  OPTIONAL { ?mother wdt:P22 ?maternalGrandfather }\n",
    "  OPTIONAL { ?mother wdt:P25 ?maternalGrandmother }\n",
    "} "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Nomisma\n",
    "\n",
    "Another excellent SPARQL endpoint is the Nomisma portal for numismatic materials. \n",
    "\n",
    "http://nomisma.org/sparql"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "%endpoint http://nomisma.org/query"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now, if you actually go to [http://query.wikidata.org/sparql](http://query.wikidata.org/sparql) you'll find a query builder with the following information already preloaded:\n",
    "\n",
    "```\n",
    "PREFIX rdf:\t<http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX bio:\t<http://purl.org/vocab/bio/0.1/>\n",
    "PREFIX crm:\t<http://www.cidoc-crm.org/cidoc-crm/>\n",
    "PREFIX dcmitype:\t<http://purl.org/dc/dcmitype/>\n",
    "PREFIX dcterms:\t<http://purl.org/dc/terms/>\n",
    "PREFIX foaf:\t<http://xmlns.com/foaf/0.1/>\n",
    "PREFIX geo:\t<http://www.w3.org/2003/01/geo/wgs84_pos#>\n",
    "PREFIX nm:\t<http://nomisma.org/id/>\n",
    "PREFIX nmo:\t<http://nomisma.org/ontology#>\n",
    "PREFIX org:\t<http://www.w3.org/ns/org#>\n",
    "PREFIX osgeo:\t<http://data.ordnancesurvey.co.uk/ontology/geometry/>\n",
    "PREFIX rdac:\t<http://www.rdaregistry.info/Elements/c/>\n",
    "PREFIX skos:\t<http://www.w3.org/2004/02/skos/core#>\n",
    "PREFIX spatial: <http://jena.apache.org/spatial#>\n",
    "PREFIX void:\t<http://rdfs.org/ns/void#>\n",
    "PREFIX xsd:\t<http://www.w3.org/2001/XMLSchema#>\n",
    "\n",
    "SELECT * WHERE {\n",
    "  ?s ?p ?o\n",
    "} LIMIT 100\n",
    "```\n",
    "\n",
    "All those prefixes are the ontologies being used to describe the materials. The `?s ?p ?o` are the subject, predicate, objects that we're going to search for. Let's run some of the [example queries](http://nomisma.org/documentation/sparql) that Nomisma can handle. Since Roman Emperors are often depicted on coins, let's see which emperors are present in Nomisma."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "%display table \n",
    "PREFIX rdf:\t<http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX bio:\t<http://purl.org/vocab/bio/0.1/>\n",
    "PREFIX crm:\t<http://www.cidoc-crm.org/cidoc-crm/>\n",
    "PREFIX dcmitype:\t<http://purl.org/dc/dcmitype/>\n",
    "PREFIX dcterms:\t<http://purl.org/dc/terms/>\n",
    "PREFIX foaf:\t<http://xmlns.com/foaf/0.1/>\n",
    "PREFIX geo:\t<http://www.w3.org/2003/01/geo/wgs84_pos#>\n",
    "PREFIX nm:\t<http://nomisma.org/id/>\n",
    "PREFIX nmo:\t<http://nomisma.org/ontology#>\n",
    "PREFIX org:\t<http://www.w3.org/ns/org#>\n",
    "PREFIX osgeo:\t<http://data.ordnancesurvey.co.uk/ontology/geometry/>\n",
    "PREFIX rdac:\t<http://www.rdaregistry.info/Elements/c/>\n",
    "PREFIX skos:\t<http://www.w3.org/2004/02/skos/core#>\n",
    "PREFIX spatial: <http://jena.apache.org/spatial#>\n",
    "PREFIX void:\t<http://rdfs.org/ns/void#>\n",
    "PREFIX xsd:\t<http://www.w3.org/2001/XMLSchema#>\n",
    "\n",
    "SELECT ?uri ?label WHERE {\n",
    "?uri a foaf:Person ;\n",
    "  skos:prefLabel ?label ;         \n",
    "  org:hasMembership ?membership .\n",
    "?membership org:role nm:roman_emperor .\n",
    "FILTER(langMatches(lang(?label), \"EN\"))\n",
    "} "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We can also do spatial queries; this one looks coins from mints around Athens.\n",
    "\n",
    "It also specifies the format in which we wants the results returned, and to write these results to a json file for further manipulation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "vscode": {
     "languageId": "python"
    }
   },
   "outputs": [],
   "source": [
    "%format json  \n",
    "%display table \n",
    "%outfile ../data/mints.json \n",
    "PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>\n",
    "PREFIX nmo: <http://nomisma.org/ontology#>\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\n",
    "\n",
    "SELECT * WHERE {\n",
    "   ?loc geo:lat ?lat ;\n",
    "        geo:long ?long .\n",
    "\n",
    "   ?mint geo:location ?loc ;\n",
    "         skos:prefLabel ?label ;\n",
    "         a nmo:Mint .\n",
    "\n",
    "   FILTER langMatches(lang(?label), \"en\")\n",
    "\n",
    "   FILTER (\n",
    "      ?lat > 37.5 && ?lat < 38.4 &&\n",
    "      ?long > 23.0 && ?long < 24.5\n",
    "   )\n",
    "}\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SPARQL",
   "language": "sparql",
   "name": "sparql"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "sparql"
   },
   "mimetype": "application/sparql-query",
   "name": "sparql",
   "pygments_lexer": "sparql-nb"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
